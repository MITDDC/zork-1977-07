<TITLE	TELL>
	<DECLARE ("VALUE" ATOM <PRIMTYPE STRING> "OPTIONAL" <OR ATOM FALSE>
		  <OR STRING FALSE> <OR STRING FALSE>)>
	<MOVE	A* AB>
LOOP	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<ADD	AB* [<(2) 2>]>
	<JUMPL	AB* LOOP>
	<HLRES	A>
	<ASH	A* -1>
	<ADDI	A* TABEND>
	<PUSHJ	P* @ (A) 1>
	<JRST	FINIS>
	<TELL4>
	<TELL3>
	<TELL2>
TABEND	<TELL1>

	<INTERNAL-ENTRY TELL1 1>		; "Push T"
	<SUBM	M* (P)>
	<PUSH	TP* <MQUOTE T> -1>
	<PUSH	TP* <MQUOTE T>>
	<CAIA>
	<INTERNAL-ENTRY TELL2 2>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FALSE>>
	<PUSH	TP* [0]>
	<CAIA>
	<INTERNAL-ENTRY TELL3 3>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FALSE>>
	<PUSH	TP* [0]>
	<CAIA>
	<INTERNAL-ENTRY TELL4 4>
	<SUBM	M* (P)>
	<INTGO>
	<MOVE	C* <MQUOTE <RGLOC OUTCHAN T>>>
	<ADD	C* GLOTOP 1>
	<MOVE	C* 1(C)>
	<MOVE	C* 1(C)>		; "CHANNEL NUMBER IN C"
	<PUSH	TP* <TYPE-WORD FIX>>	; "SAVE ON STACK"
	<PUSH	TP* C>
	<MOVE	B* -8(TP)>		; "FIRST STRING IN B"
	<HRRZ	A* -9(TP)>		; "LENGTH IN A"
	<*CALL	SIOT>
	<JRST	DONE>
	<INTGO>
	<MOVE	C* (TP)>		; "GET CHANNEL # BACK IN CASE OF INTRP"
	<MOVEI	D* -5(TP)>		; "POINTER TO BLOCK OF 2"
	<HRLI	D* -4>
PRTLOP	<MOVE	A* (D)>			; "TYPE WORD"
	<GETYP	O* A>
	<CAIN	O* <TYPE-CODE FALSE>>	; "IS IT FALSE?"
	<JRST	DONE>
	<TLZ	A* -1>
	<MOVE	B* 1(D)>
	<*CALL	SIOT>			; "DO PRINT"
	<JRST	DONE>
	<ADD	D* [<(2) 2>]>		; "MOVE TO NEXT"
	<JUMPL	D* PRTLOP>
DONE	<HRRZ	A* <MQUOTE <STRING <ASCII 13> <ASCII 10>>> -1>
	<MOVE	B* <MQUOTE <STRING <ASCII 13> <ASCII 10>>>>
	<*CALL	SIOT>			; "PRINT CRLF"
	<JFCL>
	<SUB	TP* [<(10) 10>]>
	<MOVE	C* <MQUOTE <RGLOC TELL-FLAG T>>>	;"SETG TELL-FLAG"
	<ADD	C* GLOTOP 1>
	<MOVE	A* <TYPE-WORD ATOM>>
	<MOVEM	A* (C)>
	<MOVE	B* <MQUOTE T>>
	<MOVEM	B* 1(C)>
	<JRST	MPOPJ>
SIOT	<SETZ>
	<#OPCODE *635157640000*>
	<C>
	<B>
	<SETZ	A>
